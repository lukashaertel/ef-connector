plugins {
	id "java"
	id "eclipse"
}

repositories {
    mavenCentral()
}

configurations {
	generate
}

ext {
	link_swagger_gen = false
	
    swagger_annotations_version = "1.5.0"
    jackson_version = "2.4.2"
    jersey_version = "1.18"
    jodatime_version = "2.3"
    junit_version = "4.8.1"
	
	supporting_files = [
		"ApiClient.java",
		"ApiException.java",
		"Configuration.java",
		"Pair.java",
		"StringUtil.java",
		"ApiKeyAuth.java",
		"Authentication.java",
		"HttpBasicAuth.java",
		"OAuth.java",
		"OAuthFlow.java"
	].join(",")
}

dependencies {
    compile "io.swagger:swagger-annotations:$swagger_annotations_version"
    compile "com.sun.jersey:jersey-client:$jersey_version"
    compile "com.sun.jersey.contribs:jersey-multipart:$jersey_version"
    compile "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    compile "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
    compile "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
	compile "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$jackson_version"
	compile "com.brsanthu:migbase64:2.2"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-joda:2.1.5"
    compile "joda-time:joda-time:$jodatime_version"
	
    testCompile "junit:junit:$junit_version"
	
	// Dependency for the code generator only
	generate "io.swagger:swagger-codegen-cli:2.1.5"
}

task generateSwagger(type: JavaExec) {
	// Could be implemented
	outputs.upToDateWhen { true }
	 
	// Run generator
	classpath = configurations.generate
	main = "io.swagger.codegen.SwaggerCodegen"

	// Skip the build-related files
	jvmArgs = ["-Dmodels", "-Dapis", "-DsupportingFiles=$supporting_files"] 

	// Configure for the API specification
	args = ["generate", "-i", "http://eurofurencewebapi.azurewebsites.net/swagger/swagger.json.txt", "-l", "java", "-o", "."]
}

task cleanGenerateSwagger(type: Delete) {
	delete "src/main/java/io/swagger/client"
}

// New task dependencies
compileJava.dependsOn generateSwagger
clean.dependsOn cleanGenerateSwagger
